CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    
    // 指令解码
    // 7个指令(a,zx,nx,zy,ny,f,no)传入ALU
    // 3个指令(dA, dD, dM)用于确定是否将ALU的输出值存入相应的内存或寄存器
    // 3个指令(j1, j2, j3)用于判断jump条件是否满足
    Instruction16(in=instruction, a=a, zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no, 
        dA=dA, dD=dD, dM=writeM, j1=j1, j2=j2, j3=j3);
    
    // 选择instruction或ALU的输出，写入寄存器A
    Not(in=instruction[15], out=Not15);
    Mux16(a=ALUout, b=instruction, sel=Not15, out=Muxout1);
    Or(a=dA, b=Not15, out=Aload);
    ARegister(in=Muxout1, load=Aload, out=A);
    
    // 确定是否将ALU的输出写入寄存器D
    DRegister(in=ALUout, load=dD, out=D);
    
    // 选择A或M，输入ALU
    Mux16(a=A, b=inM, sel=a, out=Muxout2);
    ALU(x=D, y=Muxout2, zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no, 
        out=outM, out=ALUout, zr=zr, ng=ng);
    
    // 根据ALU的zr(是否为0)、ng(是否为负值)判断ALU输出值是否为正值(ps)
    Or(a=ng, b=zr, out=outps);
    Not(in=outps, out=ps);
    
    // 根据zr,ng,ps和j1,j2,j3做出判断，是否执行跳转(PCload=1代表跳转)
    And(a=ng, b=j1, out=Andout1);
    And(a=zr, b=j2, out=Andout2);
    And(a=ps, b=j3, out=Andout3);
    Or(a=Andout1, b=Andout2, out=Orout);
    Or(a=Andout3, b=Orout, out=PCload);
    
    // 根据各个输入，创建计数器
    PC(in=A, load=PCload, inc=true, reset=reset, out=pc16);
    
    // 将16位的地址和计数器的数值转换位15位
    Bit16to15(in=A, out=addressM);
    Bit16to15(in=pc16, out=pc); 
}